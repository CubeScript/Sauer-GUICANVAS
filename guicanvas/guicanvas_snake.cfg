// GUICANVAS: snake demo by @SalatielSauer (06/09/2023)

_tileSize = 1;
_tileCount = (- (div $guicanvas.width $_tileSize) 2);
_snake = [[0 10] [0 9] [0 8]]
_apple = [15 15]
_yVelocity = 0
_xVelocity = 1
_lavaFrames = (loopconcat i 25 [result $i])
_lavaFrame = 0
_RENDER = [
	local headX headY
	_lavaFrame = (max 0 (at $_lavaFrames (+ (indexof $_lavaFrames $_lavaFrame) 1)))
	guicanvas.fillRect 0 0 1 $guicanvas.width (format "<offset:%1>packages/dg/floor_lava2.jpg" (* $_lavaFrame 20))
	guicanvas.fillRect 0 0 $guicanvas.width 1 (format "<offset:%1>packages/dg/floor_lava2.jpg" (* $_lavaFrame 20))
	guicanvas.fillRect $guicanvas.width 0 1 $guicanvas.width (format "<offset:%1>packages/dg/floor_lava2.jpg" (* $_lavaFrame 20))
	guicanvas.fillRect $guicanvas.width 30 $guicanvas.width 1 (format "<offset:%1>packages/dg/floor_lava2.jpg" (* $_lavaFrame 20))
	guicanvas.fillRect 31 0 1 32 (format "<offset:%1>packages/dg/floor_lava2.jpg" (* $_lavaFrame 20))
	// move the snake
	headX = (+ (at (at $_snake 0) 0) $_xVelocity)
	headY = (+ (at (at $_snake 0) 1) $_yVelocity)

	_snake = (format "[%1 %2] %3" $headX $headY (sublist $_snake 0 (- (listlen $_snake) 1))) // unshift + pop

	// check for collision with apple
	if (&& (= $headX (at $_apple 0)) (= $headY (at $_apple 1))) [
		_apple = [-1 -1]
		sleep (max 0 (- 3000 (* (listlen $_snake) 100))) [
			_apple = (format "%1 %2" (rnd $_tileCount 1) (rnd $_tileCount 1)) // new apple
			sound 62
		]
		append _snake "[]"
		sound 60
		if (= (- (listlen $_snake) 3) $_snakeRec) [
			sound 12
			clearsleep
			_apple = (format "%1 %2" (rnd $_tileCount 1) (rnd $_tileCount 1)) // new apple
			sound 62
			guicanvas.background = (format "<mad:%1/%2/%3>packages/textures/default.png" (divf (rnd 100) 100) (divf (rnd 100) 100) (divf (rnd 100) 100))

		]
	]

	// check for collisions with walls or self
	local wallCollision
	wallCollision = (|| (<= $headX 0) (<= $headY 0) (>= $headX $_tileCount) (>= $headY $_tileCount))
	if (|| (_checkSnakeCollision $headX $headY) $wallCollision) [
		_snakeDead = 1
		sound (rnd 47 33)
		sound 63
		if $wallCollision [sound 65] // toast
	]

	if (! $_snakeDead) [
		// draw apple
		guicanvas.fillRect (* (at $_apple 0) $_tileSize) (* (at $_apple 1) $_tileSize) $_tileSize $_tileSize 1 0 0
	]

	// draw snake
	loop i (listlen $_snake) [
		local color
		color = (? (>= (listlen $_snake) 32) (mod $i 2) (divf 1 $i))
		guicanvas.fillRect (* (at (at $_snake $i) 0) $_tileSize) (* (at (at $_snake $i) 1) $_tileSize) $_tileSize $_tileSize (format "<mad:%1/1/%1>%2" $color (? $_snakeDead "<colormask:1>packages/hud/blip_red_skull.png" (? (=s $i 0) (format "packages/icons/%1.jpg" (playermodelicon)) "packages/skyboxes/white_ft.jpg")))
	]

]

_checkSnakeCollision = [
	local x y collision
	x = $arg1
	y = $arg2
	collision = 0
	loop i (listlen $_snake) [
		i = (+ $i 1)
		if (= $collision 0) [
			collision = (&& (= (at (at $_snake $i) 0) $x) (= (at (at $_snake $i) 1) $y))
			i = (listlen $_snake)
		]
	]
	result $collision
]

_resetGame = [
	local startpos
	startpos = (rnd 30 1)
	_snakeDead = 0
	_snake = (format "[3 %1] [2 %1] [1 %1]" $startpos (- $startpos 1) (- $startpos 2) (- $startpos 3))
	_apple = [15 15]
	_xVelocity = 1
	_yVelocity = 0
	guicanvas.background = (format "<mad:%1/%2/%3>packages/textures/default.png" (divf (rnd 100) 100) (divf (rnd 100) 100) (divf (rnd 100) 100))

]


bind up [
	if (= $_yVelocity 0) [
		_xVelocity = 0
		_yVelocity = -1
	]
]

bind down [
	if (= $_yVelocity 0) [
		_xVelocity = 0
		_yVelocity = 1
	]
]

bind left [
	if (= $_xVelocity 0) [
		_xVelocity = -1
		_yVelocity = 0
	]
]

bind right [
	if (= $_xVelocity 0) [
		_xVelocity = 1
		_yVelocity = 0
	]
]

guicanvas.size 32 32
guicanvas.perspective 0 15 // pitch, fov
guicanvas.background = (format "<mad:%1/%2/%3>packages/textures/default.png" (divf (rnd 100) 100) (divf (rnd 100) 100) (divf (rnd 100) 100))
guicanvas.refreshRate = 50
guicanvas.refresh
if (=s (getalias _snakeRec) "") [_snakeRec = 0]
_snakeDead = 0
guicanvas.play [
	if (! $_snakeDead) [
		_RENDER
		guicanvas.refresh
		_y = (-f (min (max (at (at $_snake 0) 1) 0) 20) 6)
		_x = (-f (at (at $_snake 0) 0) 6)
		_y = (-f (+f 2 (divf (at (at $_snake 0) 1) 2)) 2)
		clearconsole
	]
] [
	local points
	points = (- (listlen $_snake) 3)
	_snakeRec = (? (> $points $_snakeRec) $points $_snakeRec)

	guialign 0 [
		guistrut -3
		if (_snakeDead) [
			guistrut -3
			guialign 0 [guiimage "<colorify:1>packages/hud/blip_red_skull.png" [] 0.5 0]
			guistrut -0.2
			guialign 0 [guititle "^f4RIP"]
			guititle (format "^f3Apples: ^f0%1^f4/%2" $points $_snakeRec)
			guistrut 0.9
			guistayopen [guibutton "try again?" [_resetGame] "../mitaman/mm-auggiedog.jpg"]
			_x = 9
			_y = 5
			guialign 0 [
				guistrut -23
				guialign 0 [
					guistrut -10.4
					guiimage (format "<rotate:%1><mad:4/3/0>packages/hud/damage.png" $points) [] 8.7 0
					guistrut -11
				]
				guistrut -25
			]
			guistrut -4.5
		] [
			guitextbox (format "^n^nApples: ^f0%1^f4/%2" $points $_snakeRec) 0 0 0xff0000
			if (&& (isconnected) (= $editing 1)) [
				guitextbox "you are in edit mode" 0 0 "0xffff00"
			]
		]
	]
] @_x @_y

//if (! $isconnected) [sleep 500 [coop; showgui guicanvas]; showgui guicanvas] // if for some reason /sound doesn't work properly in the main menu